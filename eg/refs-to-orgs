#!/usr/bin/env perl

# Convert organizations in bibliographic entries (which are
# of type 'Report') into orgs.  Associate those orgs with
# the reference from which they came.

use v5.14;

use Tuba::Client;
use Data::Dumper;
use Encode;

my $c = Tuba::Client->connect(url => $ARGV[0]);
my $report = $ARGV[1] || 'nca3';

sub split_orgs {
    my $str = shift;
    my @orgs;

    @orgs = split/,/, $str;
    do {
          s/^ +//g;
          s/ +$//g;
          s/^//;
          s/^and //;
          s/; //;
          s/\.$//;
          s/^U\.?S\.? /U.S. /;

      }
      for @orgs;
    return [ grep defined && length, @orgs ];
}


for my $ref ($c->get("/report/$report/reference?all=1")) {
    next unless $ref->{attrs}{reftype} eq 'Report';
    my $reference_identifier = $ref->{identifier};
    my $organizations = split_orgs($ref->{attrs}{Institution} || $ref->{Author});
    for my $organization_name (@$organizations) {

        my $org = $c->post_quiet("/organization/lookup/name", {
                name => $organization_name } );
        if ($org) {
            say "Found ".encode('UTF-8',$organization_name);
        } else   {
            say "Creating ".encode('UTF-8',$organization_name);
            $org = $c->post("/organization", { name => $organization_name }) or do {
                warn $c->error;
                next;
            };
        }

        # Now add this as a contributor to the publication, including the reference.
        my $pub = $c->get("/publication/$ref->{child_publication_id}");

        my $add_contributor_uri = $pub->{uri};
        $add_contributor_uri =~ s[/report][/report/contributors] or do {
            die "could not match update_contributors in $add_contributor_uri"
        };
        say "posting to $add_contributor_uri";
        $c->post($add_contributor_uri => {
                organization_identifier => $org->{identifier},
                role                    => 'author',
                reference_identifier    => $reference_identifier
            }) or warn $c->error;
        say "posted to $add_contributor_uri"; 
    }
}


